config {
  type: "table",
  bigquery: {
    partitionBy: "DATE(session_start_timestamp)"
  },
  tags: ["hourly"],
  schema: "dataform_data",
  description: "Sessions contain a combined view of tracks and pages from segment. Each session is a period of sustained activity, with a new session starting after a 30min+ period of inactivity. Each session contains a repeated field of records which are either tracks or pages. Common fields are extracted out into the top level and type specific fields are kept within two structs: records.track and records.page",
  columns: {
    session_id: "Unique identifies of the session",
    session_index: "A session counter for each user. session_index=1 is the first session for that users",
    session_start_timestamp: "Timestamp of the first event in the session",
    session_end_timestamp: "Timestamp of the last event in the session",
    context_ip: "The IP address the session came from"
  }
}

SELECT
  session_id,
  session_index,
  MIN(timestamp) AS session_start_timestamp,
  MAX(timestamp) AS session_end_timestamp,
  ANY_VALUE(context_ip) AS context_ip,
  ANY_VALUE(user_id) AS user_id,
  STRUCT(
    COUNT(tracks_info.track_id) AS total_tracks,
    COUNT(pages_info.page_id) AS total_pages,
    TIMESTAMP_DIFF(MAX(timestamp), MIN(timestamp), MILLISECOND) AS duration_millis
  ) AS stats,
  ARRAY_AGG(
    STRUCT(
      timestamp,
      context_page_url,
      context_page_path,
      tracks_info AS track,
      pages_info AS page
    )
  ) AS records
FROM
  ${ref("sessionized_records")}
GROUP BY
  session_id,
  session_index